file = {
    SOI ~ buffer ~ setup ~ turn+ ~ EOI
}
setup = {
	"SETUP" ~ buffer ~ eat_line ~ eat_line ~ handicap ~ scenario ~ optionals ~ eat_line ~ 
	country_change+ ~ buffer
}
handicap = {"Handicap influence: US +2" ~ NEWLINE} // Todo check this
scenario = {"Scenario: Standard" ~ NEWLINE}
optionals = {"Optional Cards Added" ~ NEWLINE}
single_turn = {SOI ~ buffer ~ turn ~ buffer ~ EOI} // Debug purposes
turn = {
	turn_headline | turn_cleanup | turn_std
}
// Todo see what happens if Defcon ends at 5 at end of turn
turn_headline = {
	"Turn" ~ number ~ "," ~ "Headline Phase" ~ NEWLINE ~
	card ~ "&" ~ card ~ NEWLINE ~
	defcon_change? ~
	("*RESHUFFLE*" ~ NEWLINE)? ~
	(side ~ "Headlines" ~ card ~ NEWLINE){2} ~ 
	(("Event: Defectors" ~ NEWLINE?) | (event ~ NEWLINE+ ~ event)) ~ buffer
}
turn_cleanup = {
	"Turn" ~ number ~ "," ~ "Cleanup" ~ NEWLINE ~ NEWLINE ~ outcome* ~ NEWLINE
}
turn_std = {
	"Turn" ~ number ~ "," ~ side ~ "AR" ~ number ~ NEWLINE ~ 
		(card ~ NEWLINE ~ outcome? ~ play_card)? ~ NEWLINE{0, 2}
} // Outcome? allows for cards like China -> Cancel Formosan Resolution
card = {
	(!("is" | "&") ~ word)+
}
play_card = {
	(("USSR gains 2 VP." ~ eat_line)? ~ card_use ~ NEWLINE)+
} // Weirdness for Flower Power

card_use = {
	event | inf | realign | coup | space | outcome
} // Use outcome for NORAD, Warsaw Pact Effect, and other such oddities
event = {
	"Event:" ~ card ~ NEWLINE ~ ("Event: " ~ eat_line)? ~ (outcome | conduct_ops)* 
} // Todo figure out conduct ops in event
conduct_ops = {inf | realign | coup | space}
inf = {
	"Place Influence (" ~ number ~ "Ops):" ~ NEWLINE ~
    	(country_change)+
}
// Todo figure out what free coup looks like
coup = {
	"Coup (" ~ number ~ "Ops):" ~ NEWLINE ~
	target ~
	pass_fail ~
	country_change* ~
	set_mil_ops? ~
	defcon_change?
}
realign = {
    "Realignment (" ~ number ~ "Ops):" ~ NEWLINE ~
        inner_realign+
}
// Unlike the coup, only one side can gain / lose influence in a single attempt
inner_realign = {target ~ rolls ~ rolls ~ country_change?}
space = {
	"Space Race (" ~ number ~ "Ops):" ~ NEWLINE ~
	space_roll ~ advance_space? ~ vp_change?
}

// Rolls 
pass_fail = {
	("SUCCESS:" | "FAILURE:") ~ number ~ eat_line
}
rolls = {
    side ~ "rolls" ~ number ~ eat_line
}
space_roll = {
	"Die roll:" ~ number ~ "--" ~ ("Success!" | "Failed!") ~ eat_line
}

// Misc 
country_status = {
	"[" ~ number ~ "]" ~ "[" ~ number ~ "]"
}
target = {
    "Target:" ~ words ~ NEWLINE
}

// Outcomes
outcome = {
	country_change | defcon_change | vp_change | start_effect | end_effect |
    set_mil_ops | war | advance_space | discard | (misc_text ~ NEWLINE) | un_missile
}
country_change = {
	side ~ number ~ "in" ~ words ~ country_status ~ NEWLINE
}
defcon_change = {"DEFCON" ~ ("degrades" | "improves") ~ "to" ~ number ~ NEWLINE}
vp_change = {
	inner_vp_change ~ "Score is" ~
    	("even." | side ~ number ~ ".") ~ NEWLINE
}
inner_vp_change = _{side ~ "gains" ~ number ~ "VP." | "No VP awarded."}
start_effect = {card ~ "is now in play." ~ NEWLINE}
end_effect = {card ~ "is no longer in play." ~ NEWLINE}
set_mil_ops = {side ~ "Military Ops to" ~ number ~ NEWLINE}
war = {
	"War in" ~ words ~ NEWLINE ~
    ("DEFEAT:" | "VICTORY:") ~ number ~ eat_line ~
    country_change{0, 2} ~ 
	vp_change? ~
    set_mil_ops
}
advance_space = {side ~ "advances to" ~ number ~ "in the Space Race." ~ NEWLINE}
discard = {side ~ "discards" ~ card ~ NEWLINE}
misc_text = {(side ~ "has no cards to" ~ ("discard" | "reveal"))}
un_missile = {side ~ ("plays" | "reveals") ~ card ~ NEWLINE ~ play_card} // Todo this

// Basic building blocks
side = { "USSR" | "US" }
buffer = _{NEWLINE*}
eat_line = _{(!NEWLINE ~ ANY)* ~ NEWLINE}
words = {word+}
word = @{char+}
number = @{("+" | "-")? ~ ASCII_DIGIT+}
char = { ASCII_ALPHANUMERIC | "/" | "-" | "*" | "\"" | "."} // Todo handle quote literal better
WHITESPACE = _{ " " }