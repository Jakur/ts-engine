file = {
    SOI ~ buffer ~ (inf | coup | realign | event) ~ buffer ~ EOI
}
turn = {
	"Turn" ~ ASCII_DIGIT+ ~ "," ~ ("Headline Phase" | "Cleanup" | (side ~ "AR" ~ ASCII_DIGIT+))
}
side = {
	"USSR" | "US"
}
card = {
	(!("is" | "&") ~ word)+
}
inf = {
	"Place Influence (" ~ number ~ "Ops):" ~ NEWLINE ~
    	(country_change)+
}

// TODO figure out what free coup looks like
coup = {
	"Coup (" ~ number ~ "Ops):" ~ NEWLINE ~
	target ~
	pass_fail ~
	country_change* ~
	set_mil_ops? ~
	defcon_change?
}
event = {
	"Event:" ~ card ~ NEWLINE ~ outcome+
}
country_change = {
	side ~ ("+" | "-") ~ number ~ "in" ~ words ~ country_status ~ NEWLINE
}
country_status = {
	"[" ~ number ~ "]" ~ "[" ~ number ~ "]"
}
realign = {
    "Realignment (" ~ number ~ "Ops):" ~ NEWLINE ~
        (target ~ rolls ~ rolls ~ country_change?)+
}
target = {
    "Target:" ~ words ~ NEWLINE
}
pass_fail = {
	("SUCCESS:" | "FAILURE:") ~ number ~ eat_line
}
rolls = {
    side ~ "rolls" ~ number ~ eat_line
}

// Outcomes
outcome = {
	country_change | defcon_change | vp_change | start_effect | end_effect |
    set_mil_ops | war 
}
defcon_change = {"DEFCON" ~ ("degrades" | "improves") ~ "to" ~ number ~ NEWLINE}
vp_change = {
	(side ~ "gains" ~ number ~ "VP." | "No VP awarded.") ~ "Score is" ~
    	("even." | side ~ number ~ ".") ~ NEWLINE
}
start_effect = {card ~ "is now in play." ~ NEWLINE}
end_effect = {card ~ "is no longer in play." ~ NEWLINE}
set_mil_ops = {side ~ "Military Ops to" ~ number ~ NEWLINE}
war = {
	"War in" ~ words ~ NEWLINE ~
    ("DEFEAT:" | "VICTORY:") ~ number ~ eat_line ~
    country_change* ~ 
    set_mil_ops
}
un_inter = {side ~ "plays" ~ card ~ NEWLINE ~ conduct_ops}
conduct_ops = {"TODO"}

// Basic building blocks
buffer = _{NEWLINE*}
eat_line = _{(!NEWLINE ~ ANY)* ~ NEWLINE}
words = {word+}
word = @{char+}
number = @{ASCII_DIGIT+}
char = { ASCII_ALPHANUMERIC | "/" | "-" | "*"}
WHITESPACE = _{ " " }